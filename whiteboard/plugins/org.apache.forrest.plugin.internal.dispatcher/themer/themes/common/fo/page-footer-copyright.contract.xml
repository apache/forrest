<?xml version="1.0"?>
<!--
Licensed to the Apache Software Foundation (ASF) under one or more
contributor license agreements.  See the NOTICE file distributed with
this work for additional information regarding copyright ownership.
The ASF licenses this file to You under the Apache License, Version 2.0
(the "License"); you may not use this file except in compliance with
the License.  You may obtain a copy of the License at

http://www.apache.org/licenses/LICENSE-2.0

Unless required by applicable law or agreed to in writing, software
distributed under the License is distributed on an "AS IS" BASIS,
WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
See the License for the specific language governing permissions and
limitations under the License.
-->
<forrest:contract xmlns:i18n="http://apache.org/cocoon/i18n/2.1"
xmlns:forrest="http://apache.org/forrest/templates/1.0"
name="page-footer-copyright">
  <description>
    <p>page-footer-copyright display the footer of the pdf..</p>
  </description>
  <!-- The following are used to construct a copyright statement -->
  <usage>
    <![CDATA[<forrest:contract name="page-footer-copyright">
      <forrest:property name="copyright">
        <year [inception="[true|false]"]>2005</year>
        <vendor>The Acme Software Foundation.</vendor>
        [<url>http://example.org/myproj/</url>]
        [<msg>All rights reserved.</msg>]
        [<property>copyright, patent, trademark...</property>]
        [<symbol>&#169;, &#174;, &#153;...</symbol>]
      </forrest:property>
    </forrest:contract>]]>
</usage>
  <forrest:template xmlns:forrest="http://apache.org/forrest/templates/1.0"
  name="page-footer-copyright" inputFormat="xsl">
    <xsl:stylesheet xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
    xmlns:fo="http://www.w3.org/1999/XSL/Format"
    xmlns:java="http://xml.apache.org/xslt/java" exclude-result-prefixes="java"
    version="1.0">
      <xsl:param name="copyright" />
      <xsl:variable name="symbol">
        <xsl:choose>
          <xsl:when test="$copyright/symbol">
            <xsl:value-of select="$copyright/symbol" />
          </xsl:when>
          <xsl:otherwise>&#169;</xsl:otherwise>
        </xsl:choose>
      </xsl:variable>
      <xsl:variable name="msg">
        <xsl:choose>
          <xsl:when test="$copyright/msg">
            <xsl:value-of select="$copyright/msg" />
          </xsl:when>
          <xsl:otherwise>All rights reserved.</xsl:otherwise>
        </xsl:choose>
      </xsl:variable>
      <xsl:variable name="property">
        <xsl:choose>
          <xsl:when test="$copyright/property">
            <xsl:value-of select="$copyright/property" />
          </xsl:when>
          <xsl:otherwise>Copyright</xsl:otherwise>
        </xsl:choose>
      </xsl:variable>
      <xsl:template match="/">
        <forrest:content>
          <forrest:part xpath="/fo/static-content">
            <fo:static-content flow-name="first-footer">
              <xsl:call-template name="info" />
            </fo:static-content>
            <fo:static-content flow-name="even-footer">
              <xsl:call-template name="info" />
            </fo:static-content>
            <fo:static-content flow-name="odd-footer">
              <xsl:call-template name="info" />
            </fo:static-content>
          </forrest:part>
        </forrest:content>
      </xsl:template>
      <xsl:template name="info">
        <xsl:variable name="text">
        <xsl:value-of select="$property" />&#160; 
        <xsl:value-of select="$symbol" />&#160; 
        <xsl:value-of select="$copyright/year" />
        <xsl:call-template name="current-year" />&#160; 
        <xsl:value-of select="$copyright/vendor" />&#160; 
        <xsl:value-of select="$msg" /></xsl:variable>
        <xsl:variable name="url" select="$copyright/url" />
        <fo:block-container font-style="italic" absolute-position="absolute"
        left="0pt" top="2pt" bottom="150pt" height="20pt" font-size="8pt">
          <xsl:if test="not($url)">
            <fo:block text-align="center" color="lightgrey">
              <xsl:value-of select="$text" />
            </fo:block>
          </xsl:if>
          <xsl:if test="$url">
            <fo:block text-align="center">
              <fo:basic-link color="lightgrey" external-destination="{$url}">
                <xsl:value-of select="$text" />
              </fo:basic-link>
            </fo:block>
            <fo:block text-align="center">
              <fo:basic-link color="lightgrey" external-destination="{$url}">
                <xsl:value-of select="$url" />
              </fo:basic-link>
            </fo:block>
          </xsl:if>
        </fo:block-container>
      </xsl:template>
      <!-- Displays the current year after the inception year (in the copyright i.e: 2002-2005)
       - the year format (yyyy by default can be indicated in the dateformat parameter,
       - the dates separator (- by default) can be indicated in the dateseparator parameter.
       For instance the following call will format the year on 2 digits and separates the dates
       with /
       (copyright 02/05)
         <xsl:call-template name="current-year">
           <xsl:with-param name="dateformat" select="'yy'"/>
           <xsl:with-param name="dateseparator" select="'/'"/>
         </xsl:call-template>
       Warning 1 : to enable inception year, inception attribute must be set to "true" in skinconfig.xml
       Fix Me ! This template is also defined in site2xhtml.xsl, We may need a stylesheet common to fo and XHTML ...
-->
      <xsl:template name="current-year">
        <xsl:param name="dateformat">yyyy</xsl:param>
        <xsl:param name="dateseparator">-</xsl:param>
        <xsl:if test="$copyright/year[@inception = 'true']">
          <xsl:variable name="tz"
          select='java:java.util.SimpleTimeZone.new(0,"GMT+00:00")' />
          <xsl:variable name="formatter"
          select="java:java.text.SimpleDateFormat.new($dateformat)" />
          <xsl:variable name="settz"
          select="java:setTimeZone($formatter, $tz)" />
          <xsl:variable name="date" select="java:java.util.Date.new()" />
          <xsl:variable name="year" select="java:format($formatter, $date)" />
          <xsl:if test="$copyright/year != $year">
            <xsl:value-of select="$dateseparator" />
            <xsl:value-of select="$year" />
          </xsl:if>
        </xsl:if>
      </xsl:template>
    </xsl:stylesheet>
  </forrest:template>
</forrest:contract>
