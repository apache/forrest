<?xml version="1.0" encoding="utf-8"?>
<!--
  Licensed to the Apache Software Foundation (ASF) under one or more
  contributor license agreements.  See the NOTICE file distributed with
  this work for additional information regarding copyright ownership.
  The ASF licenses this file to You under the Apache License, Version 2.0
  (the "License"); you may not use this file except in compliance with
  the License.  You may obtain a copy of the License at

      http://www.apache.org/licenses/LICENSE-2.0

  Unless required by applicable law or agreed to in writing, software
  distributed under the License is distributed on an "AS IS" BASIS,
  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
  See the License for the specific language governing permissions and
  limitations under the License.
-->
<forrest:contract name="nav-section"
xmlns:forrest="http://apache.org/forrest/templates/1.0">
  <description>
    <p>
    <strong>nav-section</strong>will output the navigation to pages within the
    current site section AKA menu.</p>
    <div class="note">
      <div class="label">Minitoc inclusion</div>
      <div class="content">
        <p>
        <code>&lt;toc&gt;</code>argument can be used to specify the minitoc
        depth. 
        <br />This argument is facultative and no minitoc will be displayed if
        it is omitted.</p>
      </div>
    </div>
  </description>
  <!--FIXME: Please add describtion about the params used in the contract -->
  <usage>
    <![CDATA[<forrest:contract name="nav-section" dataURI="cocoon://#{$cocoon/parameters/getRequest}.navigation.xml">
  [<forrest:property name="nav-section-toc" nugget="get.toc">
    <jx:import uri="cocoon://#{$cocoon/parameters/getRequest}.toc.xml"/>
  </forrest:property>]
  [<forrest:property name="toc" max-depth="2"/>]
  [<forrest:property name="expanding">false</forrest:property>]
</forrest:contract>]]>
</usage>
  <forrest:template xmlns:forrest="http://apache.org/forrest/templates/1.0"
  name="nav-section" inputFormat="xsl">
    <xsl:stylesheet version="1.1"
    xmlns:xsl="http://www.w3.org/1999/XSL/Transform">
      <xsl:param name="defaultVariables" select="'test.html'" />
      <xsl:variable name="root"
      select="$defaultVariables/*[@name='root']/@value" />
      <xsl:param name="expanding" select="'true'" />
      <xsl:param name="nav-section" select="/" />
      <xsl:param name="nav-section-toc" select="'test'" />
      <xsl:param name="toc" select="'noToc'" />
      <xsl:variable name="max-depth">
        <xsl:choose>
          <xsl:when test="$toc = 'noToc'">0</xsl:when>
          <xsl:otherwise>
            <xsl:value-of select="$toc/@max-depth" />
          </xsl:otherwise>
        </xsl:choose>
      </xsl:variable>
      <xsl:template match="/">
        <forrest:content>
          <forrest:part>
            <xsl:comment>+ |start 
            <xsl:value-of select="$max-depth" />menu +</xsl:comment>
            <xsl:if test="$nav-section/navigation/menu/div[@id='menu']/ul/li">
              <xsl:call-template name="menu">
                <xsl:with-param name="menuRoot"
                select="$nav-section/navigation/menu" />
              </xsl:call-template>
            </xsl:if>
            <xsl:comment>+ |end menu +</xsl:comment>
          </forrest:part>
          <xsl:if test="$expanding = 'true'">
            <forrest:part xpath="/html/head">
              <script type="text/javascript" src="{$root}themes/menu.js">
              &#160;</script>
            </forrest:part>
          </xsl:if>
        </forrest:content>
      </xsl:template>
      <xsl:template name="menu">
        <xsl:param name="menuRoot" value="'test'" />
        <xsl:comment>+ |start Menu +</xsl:comment>
        <div id="nav-section">
          <ul>
            <!--menu - inner-->
            <xsl:for-each select="$menuRoot/div[@id='menu']/ul/li">
              <xsl:call-template name="innermenuli">
                <xsl:with-param name="id" select="concat('1.', position())" />
              </xsl:call-template>
            </xsl:for-each>
          </ul>
        </div>
      </xsl:template>
      <xsl:template name="innermenuli">
        <xsl:param name="id" />
        <xsl:variable name="tagid">
          <xsl:choose>
            <xsl:when test="descendant-or-self::node()/li/div/@class='current'">

              <xsl:value-of select="concat('menu_selected_',$id)" />
            </xsl:when>
            <xsl:otherwise>
              <xsl:value-of select="concat('menu_',concat(font,$id))" />
            </xsl:otherwise>
          </xsl:choose>
        </xsl:variable>
        <xsl:variable name="selected">
          <xsl:choose>
            <xsl:when test="descendant-or-self::node()/li/div/@class='current'">

              <xsl:text>
selected
</xsl:text>
            </xsl:when>
            <xsl:otherwise></xsl:otherwise>
          </xsl:choose>
        </xsl:variable>
        <li class="pagegroup{normalize-space($selected)}" id="{$tagid}Title">
          <span>
            <xsl:if test="$expanding = 'true'">
              <xsl:attribute name="onclick">SwitchMenu(' 
              <xsl:value-of select="normalize-space($tagid)" />')</xsl:attribute>
            </xsl:if>
            <xsl:value-of select="h1" />
          </span>
          <ul class="{$selected}menuitemgroup" id="{$tagid}">
            <xsl:for-each select="ul/li">
              <xsl:choose>
                <xsl:when test="a">
                  <li class="menuitem">
                    <a href="{a/@href}" title="{a/@title}">
                      <xsl:value-of select="a" />
                    </a>
                  </li>
                </xsl:when>
                <xsl:when test="div/@class='current'">
                  <li class="menupage">
                    <div class="menupagetitle">
                      <xsl:value-of select="div" />
                    </div>
                    <xsl:if test="$max-depth&gt;0">
                      <ul>
                        <li class="menupageitemgroup">
                          <xsl:for-each select="$nav-section-toc//tocitems/tocitem">

                            <div class="menupageitem">
                              <xsl:choose>
                                <xsl:when test="string-length(normalize-space(@title))&gt;15">

                                  <a href="{@href}"
                                  title="{normalize-space(@title)}">
                                  <xsl:value-of select="substring(@title,0,20)" />...</a>
                                </xsl:when>
                                <xsl:otherwise>
                                  <a href="{@href}"
                                  title="{normalize-space(@title)}">
                                    <xsl:value-of select="@title" />
                                  </a>
                                </xsl:otherwise>
                              </xsl:choose>
                            </div>
                          </xsl:for-each>
                        </li>
                      </ul>
                    </xsl:if>
                  </li>
                </xsl:when>
                <xsl:otherwise>
                  <xsl:call-template name="innermenuli">
                    <xsl:with-param name="id"
                    select="concat($id, '.', position())" />
                  </xsl:call-template>
                </xsl:otherwise>
              </xsl:choose>
            </xsl:for-each>
          </ul>
        </li>
      </xsl:template>
    </xsl:stylesheet>
  </forrest:template>
</forrest:contract>
