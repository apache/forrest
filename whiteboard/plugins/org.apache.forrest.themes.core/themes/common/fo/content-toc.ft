<?xml version="1.0"?>
<!--
Licensed to the Apache Software Foundation (ASF) under one or more
contributor license agreements.  See the NOTICE file distributed with
this work for additional information regarding copyright ownership.
The ASF licenses this file to You under the Apache License, Version 2.0
(the "License"); you may not use this file except in compliance with
the License.  You may obtain a copy of the License at

http://www.apache.org/licenses/LICENSE-2.0

Unless required by applicable law or agreed to in writing, software
distributed under the License is distributed on an "AS IS" BASIS,
WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
See the License for the specific language governing permissions and
limitations under the License.
-->
<forrest:contract
  xmlns:i18n="http://apache.org/cocoon/i18n/2.1"
  xmlns:forrest="http://apache.org/forrest/templates/1.0"
  name="content-toc">
  <description>
    <p>
      content-toc will output the table of content.
    </p>

    <p>
      @toc-max-depth - Specifies how many "section" levels need to be included
      in the generated Table of Contents (TOC).
    </p>
  </description>
  <usage>
<![CDATA[<forrest:contract name="content-toc" dataURI="cocoon://#{$getRequest}.toc.xml">
      <forrest:property name="max-depth">2</forrest:property>
      <forrest:property name="page-break-after">[true|false]</forrest:property>
    </xsl:forrest:contract>]]>
  </usage>
  <forrest:template
    xmlns:forrest="http://apache.org/forrest/templates/1.0"
    name="content-toc" inputFormat="xsl">
    <xsl:stylesheet
      xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
      xmlns:fo="http://www.w3.org/1999/XSL/Format"
      version="1.0">
      <xsl:param name="max-depth" select="2"/>
      <xsl:param name="page-break-after" select="'true'"/>
      <xsl:include href="lm://pdf.transform.helper.pageBreaks"/>
      <xsl:template match="/">
        <forrest:content>
          <forrest:part xpath="/fo/xsl-region-body/title">
            <fo:block
              font-family="sans-serif"
              font-size="12pt"
              font-weight="bold"
              space-after="5pt"
              space-before="5pt"
              text-align="justify"
              width="7.5in">
              <xsl:call-template name="insertPageBreaks"/>
              <!-- insert i18n stuff here -->
              <xsl:text>Table of contents</xsl:text>
            </fo:block>
            <fo:block font-family="serif" font-size="12pt" space-after="5pt"
              space-before="0pt" text-align="justify" width="7.5in">
              <xsl:if test="$page-break-after">
                <xsl:attribute name="break-after">page</xsl:attribute>
              </xsl:if>
              <xsl:apply-templates select="/tocitems/tocitem"/>
            </fo:block>
          </forrest:part>
        </forrest:content>
      </xsl:template>
      <xsl:template match="tocitem">
        <!-- FIXME: see bug FOR-640 -->
        <xsl:param name="depth" select="'1'"/>
        <fo:block space-before="5pt" text-align-last="justify" start-indent=".5em"
          text-indent=".5em">
          <fo:inline>
            <xsl:variable name="id" select="substring(@href,2)"/>
            <fo:basic-link internal-destination="{$id}">
              <xsl:value-of
                select="substring('&#160;&#160;&#160;&#160;&#160;&#160;&#160;&#160;&#160;&#160;', 0, 2 * $depth - 1)"/>
              <fo:inline font-size="11pt">
                <xsl:number count="tocitem" format="1.1.1.1.1.1.1"
                  level="multiple"/>
              </fo:inline>
              <xsl:text> </xsl:text>
              <xsl:value-of select="@title"/>
              <fo:leader leader-pattern="dots"/>
              <fo:page-number-citation ref-id="{$id}"/>
            </fo:basic-link>
          </fo:inline>
          <xsl:if test="$max-depth > $depth">
            <xsl:apply-templates select="tocitem">
              <xsl:with-param name="depth" select="$depth + 1"/>
            </xsl:apply-templates>
          </xsl:if>
        </fo:block>
      </xsl:template>
    </xsl:stylesheet>
  </forrest:template>
</forrest:contract>
