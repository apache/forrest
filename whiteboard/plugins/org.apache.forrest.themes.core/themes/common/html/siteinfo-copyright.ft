<?xml version="1.0" encoding="UTF-8"?>
<!--
  Licensed to the Apache Software Foundation (ASF) under one or more
  contributor license agreements.  See the NOTICE file distributed with
  this work for additional information regarding copyright ownership.
  The ASF licenses this file to You under the Apache License, Version 2.0
  (the "License"); you may not use this file except in compliance with
  the License.  You may obtain a copy of the License at

      http://www.apache.org/licenses/LICENSE-2.0

  Unless required by applicable law or agreed to in writing, software
  distributed under the License is distributed on an "AS IS" BASIS,
  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
  See the License for the specific language governing permissions and
  limitations under the License.
-->
<forrest:contract xmlns:forrest="http://apache.org/forrest/templates/1.0"
  name="siteinfo-copyright">
  <description>
    <p>
      <strong>siteinfo-copyright</strong>
      <br/>
      The following properties are used to construct a copyright statement.
    </p>
  </description>
  <usage>
<![CDATA[<forrest:contract name="siteinfo-copyright">
  <forrest:property name="siteinfo-copyright">
    <copyright>
      <year [inception="[true|false]"]>2005</year>
      <vendor>The Acme Software Foundation.</vendor>
      [<copyright-link>http://...</copyright-link>]
      [<msg>All rights reserved.</msg>]
    </copyright>
  </forrest:property>
</forrest:contract>]]>
  </usage>
  <forrest:template xmlns:forrest="http://apache.org/forrest/templates/1.0"
    name="siteinfo-copyright" inputFormat="xsl">
    <xsl:stylesheet version="1.1"
      xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
      xmlns:java="http://xml.apache.org/xslt/java"
      exclude-result-prefixes="java">
      <xsl:param name="siteinfo-copyright"/>
      <xsl:param name="use-br">false</xsl:param>
      <xsl:template match="/">
        <forrest:content>
          <forrest:part>
            Copyright &#169;
            <xsl:text> </xsl:text>
            <xsl:value-of select="$siteinfo-copyright/copyright/year"/>
            <xsl:call-template name="current-year">
              <xsl:with-param name="copyrightyear" select="$siteinfo-copyright/copyright/year"/>
            </xsl:call-template>
<xsl:text> </xsl:text>
            <xsl:choose>
              <xsl:when test="$siteinfo-copyright/copyright/copyright-link"><a>
                <xsl:attribute name="href">
                  <xsl:value-of select="$siteinfo-copyright/copyright/copyright-link"/>
                </xsl:attribute>
                <xsl:value-of select="$siteinfo-copyright/copyright/vendor"/></a>
              </xsl:when>
              <xsl:otherwise>
                <xsl:value-of select="$siteinfo-copyright/copyright/vendor"/>
              </xsl:otherwise>
            </xsl:choose>
<xsl:text> </xsl:text>
           <xsl:if test="$use-br='true'">
             <br />
           </xsl:if>
            <xsl:value-of select="$siteinfo-copyright/copyright/msg"/>
          </forrest:part>
        </forrest:content>
      </xsl:template>
      <xsl:template name="current-year">
<!-- Displays the current year after the inception year (in the copyright i.e: 2002-2005)
       - the copyright year (2005 by default) can be indicated in the copyrightyear parameter,
       - the year format (yyyy by default) can be indicated in the dateformat parameter,
       - the dates separator (- by default) can be indicated in the dateseparator parameter.
       For instance the following call will format the year on 2 digits and separates the dates
       with /
       (copyright 02/05)
         <xsl:call-template name="current-year">
           <xsl:with-param name="copyrightyear" select="'02'"/>
           <xsl:with-param name="dateformat" select="'yy'"/>
           <xsl:with-param name="dateseparator" select="'/'"/>
         </xsl:call-template>
       Warning, to enable inception year, inception attribute must be set to "true" in copyright/year/@inception
      -->
        <xsl:param name="copyrightyear">2005</xsl:param>
        <xsl:param name="dateformat">yyyy</xsl:param>
        <xsl:param name="dateseparator">-</xsl:param>
        <xsl:if test="$copyrightyear[@inception = 'true']">
          <xsl:variable name="tz" select='java:java.util.SimpleTimeZone.new(0,"GMT+00:00")' />
          <xsl:variable name="formatter" select="java:java.text.SimpleDateFormat.new($dateformat)"/>
          <xsl:variable name="settz" select="java:setTimeZone($formatter, $tz)" />
          <xsl:variable name="date" select="java:java.util.Date.new()"/>
          <xsl:variable name="year" select="java:format($formatter, $date)"/>
          <xsl:if test="$copyrightyear != $year">
            <xsl:value-of select="$dateseparator"/>
            <xsl:value-of select="$year"/>
          </xsl:if>
        </xsl:if>
      </xsl:template>
    </xsl:stylesheet>
  </forrest:template>
</forrest:contract>
