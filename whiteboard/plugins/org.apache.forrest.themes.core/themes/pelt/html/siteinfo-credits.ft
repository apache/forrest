<?xml version="1.0" encoding="UTF-8"?>
<!--
  Licensed to the Apache Software Foundation (ASF) under one or more
  contributor license agreements.  See the NOTICE file distributed with
  this work for additional information regarding copyright ownership.
  The ASF licenses this file to You under the Apache License, Version 2.0
  (the "License"); you may not use this file except in compliance with
  the License.  You may obtain a copy of the License at

      http://www.apache.org/licenses/LICENSE-2.0

  Unless required by applicable law or agreed to in writing, software
  distributed under the License is distributed on an "AS IS" BASIS,
  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
  See the License for the specific language governing permissions and
  limitations under the License.
-->
<forrest:contract xmlns:forrest="http://apache.org/forrest/templates/1.0"
  name="siteinfo-credits">
  <description> siteinfo-credits - Template will output the siteinfo-credits
    with pelt look'n feel. </description>

  <usage><![CDATA[<forrest:contract name="siteinfo-credits">
    [<forrest:property name="box-location"></forrest:property>
     <forrest:property name="top-separator">[true|<strong>false</strong>]</forrest:property>
     <forrest:property name="use-role-as-prefix">[true|<strong>false</strong>]</forrest:property>
     <forrest:property name="siteinfo-credits">
      <credits>
        <credit>
          <name>Built with Apache Forrest</name>
          <url>http://forrest.apache.org/</url>
          <image>images/built-with-forrest-button.png</image>
          <width>88</width>
          <height>31</height>
        </credit>
      </credits>
    </forrest:property>]
  </forrest:contract>]]></usage>

  <forrest:template xmlns:forrest="http://apache.org/forrest/templates/1.0"
    name="siteinfo-credits" inputFormat="xsl">
    <xsl:stylesheet version="1.1"
      xmlns:xsl="http://www.w3.org/1999/XSL/Transform">
        <xsl:param name="defaultVariables" select="'test.html'"/>
        <xsl:param name="siteinfo-credits"/>
        <xsl:param name="box-location"></xsl:param>
        <xsl:param name="top-separator">false</xsl:param>
        <xsl:param name="use-role-as-prefix">false</xsl:param>
        <xsl:param name="use-br">false</xsl:param>
        <xsl:variable name="filename" select="$defaultVariables/*/*[@name='filename']/@value"/>
        <xsl:variable name="root" select="$defaultVariables/*/*[@name='root']/@value"/>

      <xsl:template match="/">
        <forrest:content>
          <forrest:part>
            <xsl:if test="$siteinfo-credits">
              <xsl:variable name="credit" 
                select="$siteinfo-credits/credits/credit[($use-role-as-prefix = 'true' and starts-with( $filename, concat( @role, '.') ) ) or starts-with( $filename, 'index') ]"/>
              <xsl:if test="$credit">
                <xsl:comment> |start siteinfo-credits </xsl:comment>
                <div id="{$box-location}">
                  <xsl:if test="$top-separator = 'true'">
                    <hr/>
                  </xsl:if>
                  <xsl:for-each select="$credit">
                    <xsl:if test="not( position()=1 ) and $use-br='true'">
                      <br/>
                    </xsl:if>
                    <xsl:variable name="name" select="normalize-space(name)"/>
                    <xsl:variable name="url" select="normalize-space(url)"/>
                    <xsl:variable name="image" select="normalize-space(image)"/>
                    <xsl:variable name="width" select="normalize-space(width)"/>
                    <xsl:variable name="height" select="normalize-space(height)"/>
                    <a href="{$url}">
                      <img alt="{$name} - logo" title="{$name}">
                        <xsl:attribute name="src">
                          <xsl:if test="not(starts-with($image, 'http://'))">
                            <xsl:value-of select="$root"/>
                          </xsl:if>
                          <xsl:value-of select="$image"/>
                        </xsl:attribute>
                        <xsl:attribute name="style">
                          <xsl:if test="$width">width: <xsl:value-of 
                            select="$width"/>px;</xsl:if>
                          <xsl:if test="$height">height: <xsl:value-of 
                            select="$height"/>px;</xsl:if>
                        </xsl:attribute>
                      </img>
                    </a>
                  </xsl:for-each>
                </div>
                <xsl:comment>+ |end siteinfo-credits +</xsl:comment>
              </xsl:if>
            </xsl:if>
          </forrest:part>
        </forrest:content>
      </xsl:template>
    </xsl:stylesheet>
  </forrest:template>
</forrest:contract>

